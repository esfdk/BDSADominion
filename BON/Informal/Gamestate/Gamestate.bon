cluster_chart GAMESTATE_CLUSTER
	indexing
		author: "Jakob Melnyk";
	explanation "The classes making up the 'dynamic' state of the game. This includes decks, hands, discard piles, etc."
	class GAMESTATE
		description "The overall 'state' of the current game."
	class PLAYER
		description "Represents a player and everything a player owns."
	class ZONES
		description "List of zones that can be targets of events."
	cluster CARD_CLUSTER
		description "The different kinds of cards."
end

class_chart GAMESTATE
	indexing
		author: "Jakob Melnyk";
	explanation "Keeps track of the players and everything the players share, such as the trash pile and the supply."
	query
		"May I have a new gamestate with this set-up?",
		"Who is the active player?",
		"Which phase is the active player in?",
		"What cards are in the trash pile?",
		"What players are in the game?",
	command
		"Make this player the active player!",
		"Start Action Phase",
		"End Action Phase",
		"Start Buy Phase",
		"End Buy Phase",
		"Do Clean-up phase",
		"Increase the amount of actions the active player has by this much!",
		"Increase the amount of buys the active player has by this much!",
		"Increase the amount of coins the active player has by this much!",
		"That player gains this card type in this zone from the supply.",
	constraint
		"Can have 2, 3 OR 4 players.",
		"Cannot end Action Phase while not in Action Phase.",
		"Cannot end Buy Phase while not in Buy Phase.",
		"The active player cannot be made the active player."
end

class_chart PLAYER
	indexing
		author: "Jakob Melnyk";
	explanation "Each player is represented by a player object that keeps track of their decks, hands, discard piles, etc."
	query
		"Can I see all your cards?",
		"How many cards do you have in your deck?",
		"How many cards do you have in your discard pile?",
		"How many cards do you have in your hand?",
		"What card is on top of your discard pile?",
		"What cards do you have in your hand?",
	command
		"Move this card from that zone to the temporary zone!",
		"Move this card from the temporary zone to that zone!",
		"Add this card to that zone!",
		"Remove this card!",
		"Draw a card."
	constraint
		"A player cannot have a card in his deck, discard pile, hand, or 'played field' that is not in his total set of cards."
end 

class_chart ZONES
	indexing
		author: "Jakob Melnyk";
	explanation "Represents the values used to refer to the zones in the player class and gamestate class."
	query
		"May I have the value 'v'?",
	constraint
		"The values allowed for this class are exactly one of 'DECK', 'DISCARD', 'HAND', 'SUPPLY', 'PLAYED', 'TRASH'."
end 